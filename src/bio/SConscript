"""Build blob I/O"""
import os
import daos_build

def scons():
    """Execute build"""
    Import('env', 'prereqs', 'control_tgts')

    env.AppendUnique(LIBPATH=[Dir('.')])

    denv = env.Clone()
    denv.AppendUnique(LINKFLAGS=["-Wl,--no-as-needed"])
    denv.Append(CPPDEFINES=['-DDAOS_PMEM_BUILD'])
    prereqs.require(denv, 'pmdk', 'spdk', 'argobots', 'protobufc', 'hwloc')

    SConscript('smd/SConscript')
    denv.AppendUnique(LIBPATH=['smd'])
    denv.AppendUnique(OBJPREFIX='b_')

    # SPDK related libs
    libs = ['spdk_env_dpdk', 'spdk_thread', 'spdk_init', 'spdk_blob']
    libs += ['rte_mempool_ring', 'rte_bus_pci', 'spdk_bdev_aio']
    libs += ['spdk_bdev_nvme', 'spdk_blob_bdev', 'spdk_vmd']

    # Other libs
    libs += ['numa', 'dl', 'smd']

    tgts = Glob('*.c') + control_tgts
    bio = daos_build.library(denv, "bio", tgts, install_off="../..", LIBS=libs)
    denv.Install('$PREFIX/lib64/daos_srv', bio)

if __name__ == "SCons.Script":
    scons()
